; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "osgPython"
#define SitePackage "osgswig-0.9.0"
#define MyAppVerName "OpenSceneGraph for Python 2.5"
#define MyAppPublisher "Hartmut Seichter, HITLab New Zealand"
#define MyAppURL "http://www.hitlabnz.org"
#define OSG_VERSION "2.3.4"
#define OSGSWIG_VERSION "0.9.0"
#define PACKAGE_REVISION "-1"

#define OSG_ROOT GetEnv("OSG_ROOT")
#define ARTOOLKIT_2_ROOT GetEnv("ARTOOLKIT_2_ROOT")
#define OSGART_ROOT GetEnv("OSGART_ROOT")

[Setup]
UsePreviousAppDir=no
AppName={#MyAppName}
AppVerName={#MyAppVerName}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={reg:HKLM\Software\Python\PythonCore\2.5\InstallPath,|{pf}\#MyAppName}Lib/site-packages/{#SitePackage}
DefaultGroupName={#MyAppName}
OutputDir=.innosetup
OutputBaseFilename={#MyAppName}-{#OSG_VERSION}{#PACKAGE_REVISION}-py25
Compression=lzma
;; Compression=zip
SolidCompression=yes
SourceDir=..\..\..\build\lib
WizardImageFile=compiler:wizmodernimage-IS.bmp
WizardSmallImageFile=compiler:wizmodernsmallimage-IS.bmp
AppVersion={#OSG_VERSION}
UninstallFilesDir={app}\uninst
LicenseFile=..\..\LICENSE.txt


[Languages]
Name: eng; MessagesFile: compiler:Default.isl

[Types]
Name: full; Description: Complete Installation; Languages: 
Name: compact; Description: Install only necessary items.
Name: custom; Description: Customized installation.; Flags: iscustom

[Components]
Name: library; Description: OpenSceneGraph Bindings; Types: full compact custom; Flags: fixed
Name: examples; Description: Examples; Types: full compact custom
Name: doc; Description: Documentation; Types: full compact custom

[Files]
;; License Files
Source: ..\..\LICENSE.txt; Components: library; DestDir: {app}; Flags: ignoreversion
Source: ..\..\COPYING.txt; Components: library; DestDir: {app}; Flags: ignoreversion

;; DLLs
Source: python\release\_*.pyd; Components: library; DestDir: {app}; Flags: ignoreversion

;; Wrapper Libraries
Components: library; Source: python\osg*.py;  DestDir: {app}; Flags: ignoreversion

;; OSG
Components: library; Source: {#OSG_ROOT}\bin\osg*.dll;  DestDir: {app}; Flags: ignoreversion
Components: library; Source: {#OSG_ROOT}\bin\ot*.dll;  DestDir: {app}; Flags: ignoreversion
Components: library; Source: {#OSG_ROOT}\bin\osgPlugins-{#OSG_VERSION}\osgdb_*.dll;  DestDir: {app}\osgPlugins-{#OSG_VERSION}; Flags: ignoreversion

; osgART
Components: library; Source: {#OSGART_ROOT}\bin\osgART.dll; DestDir: {app}\osgPlugins-{#OSG_VERSION}; Flags: ignoreversion
Components: library; Source: {#OSGART_ROOT}\bin\osgart_*_artoolkit.dll; DestDir: {app}\osgPlugins-{#OSG_VERSION}; Flags: ignoreversion
Components: library; Source: {#OSGART_ROOT}\bin\DSVL.dll; DestDir: {app}\..\..\..
Components: library; Source: {#OSGART_ROOT}\bin\libARvideo.dll;DestDir: {app}\..\..\..

; put a path file
Components: library; Source: python\osg.pth; DestDir: {app}\..; Flags: ignoreversion

;; Examples --
Source: ..\..\examples\python\*.pyw; Components: examples; DestDir: {app}\examples\python; Flags: ignoreversion
Source: ..\..\examples\python\*.py; Components: examples; DestDir: {app}\examples\python; Flags: ignoreversion

;; Data for osgART
Source: {#OSGART_ROOT}\bin\Data\*.dat; Components: examples; DestDir: {app}\examples\python\data; Flags: ignoreversion
Source: {#OSGART_ROOT}\bin\Data\patt.*; Components: examples; DestDir: {app}\examples\python\data; Flags: ignoreversion
Source: {#ARTOOLKIT_2_ROOT}\patterns\pattHiro.pdf; Components: examples; DestDir: {app}\examples\python\data; Flags: ignoreversion

;; Cow!
;; Source: ..\..\examples\python\models\*.osg; Components: examples; DestDir: {app}\examples\python; Flags: ignoreversion
;; Source: examples\python\Images\*.rgb; Components: examples; DestDir: {app}\examples\python\Images; Flags: ignoreversion


;; Documentation
;; Source: src\*.i; Components: code; DestDir: {app}\src; Flags: recursesubdirs
;; Source: ..\..\doc\python\doc\html\osgPython.ch?; Components: doc; DestDir: {app}\doc

[Icons]
Name: {group}\Uninstall {#MyAppName}; Filename: {app}\{uninstallexe}
Name: {group}\API Reference; Filename: {app}\doc\osgPython.chm; Components: doc

;; Example viewer
Name: {group}\Examples\Viewer\Run; Filename: {app}\examples\python\viewer.py; Parameters: ; WorkingDir: {app}\examples\python; Components: examples
Name: {group}\Examples\Viewer\Edit; Filename: {win}\notepad.exe; Components: examples; Parameters: {app}\examples\python\viewer.py; WorkingDir: {app}\examples\python

;; Example wxWidgets viewer
Name: {group}\Examples\wxWidgets & OSG\Run; Filename: {app}\examples\python\wx_viewer.pyw; Parameters: ; WorkingDir: {app}\examples\python; Components: examples
Name: {group}\Examples\wxWidgets & OSG\Edit; Filename: {win}\notepad.exe; Components: examples; Parameters: {app}\examples\python\wx_viewer.pyw; WorkingDir: {app}\examples\python

;; Example osgART
Name: {group}\Examples\osgART AR Viewer\Run; Filename: {app}\examples\python\osgart.py; Parameters: ; WorkingDir: {app}\examples\python; Components: examples
Name: {group}\Examples\osgART AR Viewer\Edit; Filename: {win}\notepad.exe; Components: examples; Parameters: {app}\examples\python\osgart.py; WorkingDir: {app}\examples\python
Name: {group}\Examples\osgART AR Viewer\Marker for Printout; Filename: {app}\examples\python\data\pattHiro.pdf; Components: examples;

[UninstallDelete]
Name: {app}\*.*; Type: filesandordirs
